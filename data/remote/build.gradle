apply from: "${rootDir}/config/dependencies/features.gradle"
apply from: "${rootDir}/config/dependencies/android.gradle"

android {
    File geolocation = new File("${rootDir}/geolocation_api_key.txt")
    String geolocationApiKey = "\"" + geolocation.text.trim() + "\""

    File weather = new File("${rootDir}/weather_api_key.txt")
    String weatherApiKey = "\"" + weather.text.trim() + "\""

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildTypes.forEach {
        it.buildConfigField 'String', 'WEATHER_API_KEY', "${weatherApiKey.trim()}"
        it.buildConfigField 'String', 'WEATHER_API_URL', "\"https://api.openweathermap.org/data/2.5/\""
        it.buildConfigField 'String', 'GEOLOCATION_API_KEY', "${geolocationApiKey.trim()}"
        it.buildConfigField 'String', 'GEOLOCATION_API_URL', "\"https://us1.locationiq.com/v1/\""
    }
}

dependencies {
    implementation project(":data:repository")

    implementation Dependencies.okHttp.loggingInterceptor
    implementation Dependencies.retrofit.retrofit
    implementation Dependencies.retrofit.moshi
    testImplementation Dependencies.test.junit
}
